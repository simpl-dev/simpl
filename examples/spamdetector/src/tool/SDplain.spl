// SpamDetector grammar without annotations

grammar ee.cyber.simplicitas.spamdetector.SD;

Program: Item+;

option Item: Rule | Condition;

Rule: "rule" Name Score ":" OrExpression;

Condition: "condition" Id ":" OrExpression;

option Primitive returns Expression:
      Contains
    | NotContains
    | Count
    | ConditionCall
    | ParenExpression
    | NotExpression;

OrExpression: AndExpression ("or" AndExpression)*;
AndExpression: Primitive ("and" Primitive)*;

ParenExpression: "(" OrExpression ")";
NotExpression: "not" Primitive;

Contains: field=Id "=" Regexp;
NotContains: Id "!=" Regexp;
Count: "count" "(" ExprList ")" "=" Num;
ExprList: OrExpression ("," OrExpression)*;
ConditionCall: Id;

terminal Regexp: "/" (~"/")* "/" "i"?;
terminal Score: "-"? Digit+ "." Digit*;
terminal Num: Digit+;
terminal Name: '"' (~'"')* '"';
fragment Digit: '0'..'9';

terminal Id: IdStart IdNext*;
fragment IdStart: 'a'..'z'|'A'..'Z'|'_';
fragment IdNext: IdStart|'0'..'9'|'-';

fragment MlComment: '/*' (~'*' | '*' ~'/')* '*/';
fragment SlComment: '//' ~('\n'|'\r')*;
hidden terminal WS: (' '|'\t'|'\r'|'\n'|SlComment|MlComment)+;